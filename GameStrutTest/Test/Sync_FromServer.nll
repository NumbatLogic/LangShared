namespace NumbatLogic
{
	class Sync_FromServer_Server : gsServer
	{
		public construct(string sxAddress, Uint16 nPort, Uint16 nVersion, string sxDatabasePath) : base (sxAddress, nPort, nVersion, sxDatabasePath)
		{
		}

		public override gsServerClient** OnCreateServerClient(Uint32 nClientId, gsClientSocket pClientSocket, gsServer pServer)
		{
			return new Sync_FromServer_ServerClient(nClientId, pClientSocket, pServer);
		}
	}

	class Sync_FromServer_ServerClient : gsServerClient
	{
		public construct(Uint32 nClientId, gsClientSocket pClientSocket, gsServer pServer) : base (nClientId, pClientSocket, pServer)
		{
		}

		public override void OnInitialJoin()
		{
			gsServerRoom* pOwnedServerRoom = own new gsServerRoom(__pServer.__nLastRoomId++, "Sync_FromServer_Room", __pServer);
			gsServerRoom pServerRoom = pOwnedServerRoom;
			__pServer.__pRoomVector.PushBack(disown pOwnedServerRoom);
			__pServer.__ClientJoin(this, pServerRoom);

			gsBlob* pTestBlob = own new gsBlob();
			pTestBlob.PackUint32(42);
			Send(pServerRoom, tthash("Test"), pTestBlob);
		}
	}

	class Sync_FromServer_Client : gsClient
	{
		public Uint32 m_nTestValue;
		
		public construct(string sxAddress, Uint16 nPort, Uint16 nVersion) : base (sxAddress, nPort, nVersion)
		{
			RegisterHandler(tthash("Test"), OnTest);
		}

		private static bool OnTest(gsClient pClient, Uint32 nSyncId, gsBlob pMessageBlob)
		{
			Sync_FromServer_Client pSyncClient = cast Sync_FromServer_Client(pClient);
			Assert::Plz(pSyncClient != null);
			Assert::Plz(pMessageBlob.UnpackUint32(ref pSyncClient.m_nTestValue));
			return true;
		}
	}

	class Sync_FromServer
	{
		public static void Run()
		{
			gsServer* pServer = own new Sync_FromServer_Server("localhost", 9877, 0, "");
			gsClient* pClient = own new Sync_FromServer_Client("localhost", 9877, 0);

			Assert::Plz(pServer.__pRoomVector.GetSize() == 0);

			GameStrutTestUtil::Update(pServer, pClient);

			Assert::Plz(pServer.__pClientVector.GetSize() == 1);
			Assert::Plz(pServer.__pRoomVector.GetSize() == 1);
			Assert::Plz(pClient.__eState == gsClient::State::CONNECTED);
			Assert::Plz(pClient.__pRoomVector.GetSize() == 1);

			Sync_FromServer_Client pSyncClient = cast Sync_FromServer_Client(pClient);
			Assert::Plz(pSyncClient != null);
			Assert::Plz(pSyncClient.m_nTestValue == 42);
		}
	}
}
